PROGRAM = { SIMPLE_STATEMENT | IF_BLOCK | WHILE_BLOCK }

SIMPLE_STATEMENT = READ_STATEMENT | PRINT_STATEMENT | DECLARATION_STATEMENT | ASSIGNMENT_STATEMENT
READ_STATEMENT = "read" IDENTIFIER_LIST "."
IDENTIFIER_LIST = [{ IDENTIFIER "," }] IDENTIFIER
PRINT_STATEMENT = "print" IDENTIFIER_OR_CONSTANT_LIST "."
IDENTIFIER_OR_CONSTANT_LIST = [{ ( IDENTIFIER | CONSTANT ) "," }] ( IDENTIFIER | CONSTANT )
DECLARATION_STATEMENT = TYPE IDENTIFIER_LIST "."
TYPE = "int" | "string"

ASSIGNMENT_STATEMENT = IDENTIFIER "=" EXPRESSION "."
EXPRESSION = EXPRESSION ( "+" | "-" ) TERM | TERM
TERM = TERM ( "*" | "/" ) FACTOR  | FACTOR
FACTOR = "(" EXPRESSION ")" | IDENTIFIER | CONSTANT

IF_BLOCK = "if" CONDITION "{" { SIMPLE_STATEMENT } "}" [ "else" "{" { SIMPLE_STATEMENT } "}" ]
CONDITION =  EXPRESSION RELATION EXPRESSION
RELATION = "<" | "<=" | "==" | "!=" | ">=" | ">"
WHILE_BLOCK = "while" CONDITION "{" { SIMPLE_STATEMENT } "}"


IDENTIFIER = LETTER { LETTER | DIGIT }
LETTER = "a" | "b" | ... | "z" | "A" | "B" | ... | "Z"
DIGIT = "0" | "1" | ... | "9"
SPECIAL_CHAR = "." | "!" | "," | "!" | "?" | ";"
CONSTANT = INTEGER | STRING

INTEGER = "0" | [ SIGN ] NON_ZERO_DIGIT { DIGIT }
NON_ZERO_DIGIT = "1" | "2" | ... | "9"
SIGN = "+" | "-"
STRING = """ [{ DIGIT | LETTER | SPECIAL_CHAR }] """